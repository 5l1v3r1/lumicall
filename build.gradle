buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'com.googlecode.libphonenumber:libphonenumber:7.1.0'
    compile 'org.opentelecoms.ddds:nominet-ddds:1.0'
    compile 'info.ganglia.gmetric4j:gmetric4j:1.0.10'
    compile 'de.rtner:PBKDF2:1.1.1'
    compile 'org.opentelecoms.sip:mjsip-fork:1.6+lumicall.2'
    compile ('org.opentelecoms.zrtp:zorg-zrtp:1.0.0+f.1') {
        // We exclude the transitive dependency Bouncy Castle and
        // use Spongy Castle instead because of the classpath issues
        // with Bouncy Castle on Android
        exclude module: 'bcprov-jdk16'
    }
    compile 'com.madgag.spongycastle:prov:1.52.0.0'
    compile ('org.opentelecoms.ice:ice4j-fork:1.0+f.4') {
        // nist-sdp and jain-sip-ri are transitive dependencies, must exclude
        // them here or we get errors about duplicate classes later.  Some
        // of these classes may be part of the Android platform.
        exclude module: 'nist-sdp'
    }
}

android {
    defaultConfig {
        versionCode 177
        versionName "1.12.5"

        minSdkVersion 8
        targetSdkVersion 8
    }

    compileSdkVersion 19
    buildToolsVersion "19.1"

    // FIXME: This project currently generates a lot of lint errors,
    // they need to be cleaned up at some point
    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile file(System.getenv('HOME') + '/.keystore')
            storePassword 'unset'
            keyAlias System.getenv('SIGNING_KEY')
            keyPassword 'unset'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            // FIXME: JNI stuff must currently be built by running
            // ndk-build before the gradle build
            jni.srcDirs = []
            jniLibs.srcDir 'libs'
        }

    }
}

task askForPassword << {
    def mypassword = new String(System.console().readPassword("Signing password: "))
    android.signingConfigs.release.storePassword = mypassword
    android.signingConfigs.release.keyPassword = mypassword
}

tasks.whenTaskAdded { task ->
    if(task.name == 'validateReleaseSigning') {
        task.dependsOn askForPassword
    }
}

